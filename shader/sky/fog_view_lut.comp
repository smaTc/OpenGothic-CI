#version 450

#extension GL_EXT_control_flow_attributes : enable
#extension GL_GOOGLE_include_directive    : enable

#include "sky_common.glsl"

layout(binding = 0) uniform sampler2D tLUT;
layout(binding = 1) uniform sampler2D mLUT;
#ifdef COMPUTE
layout(binding = 2) uniform sampler2D textureSm1;
layout(binding = 3) uniform writeonly image3D fogLut;
layout(binding = 4, std140) uniform UboScene {
  vec3  sunDir;
  // float padd0;
  mat4  viewProject;
  mat4  viewProjectInv;
  mat4  viewShadow[2];
  vec3  ambient;
  vec4  sunCl;
  vec4  frustrum[6];
  vec3  clipInfo;
  // float padd1;
  vec3  camPos;
  // float padd2;
  vec2  screenResInv;
  } scene;
#endif

#ifdef COMPUTE
layout(local_size_x = 8, local_size_y = 8) in;
#else
layout(location = 0) in  vec2 inPos;
layout(location = 0) out vec4 outColor;
#endif

void store(ivec3 at, vec3 lum, vec3 trans) {
  const float t = (trans.x+trans.y+trans.z)/3.0;
#ifdef COMPUTE
  imageStore(fogLut, at, vec4(lum,t));
#else
  outColor = vec4(lum,t);
#endif
  }

#ifdef COMPUTE
vec4 shadowSample(in sampler2D shadowMap, vec2 shPos) {
  shPos.xy = shPos.xy*vec2(0.5,0.5)+vec2(0.5);
  return textureGather(shadowMap,shPos);
  }

float shadowResolve(in vec4 sh, float z) {
  z  = max(0,z);
  sh = step(sh,vec4(z));
  return 0.25*(sh.x+sh.y+sh.z+sh.w);
  }

float calcShadow(vec3 shPos1) {
  vec4  lay1 = shadowSample(textureSm1,shPos1.xy);
  float v1   = shadowResolve(lay1,shPos1.z);
  if(abs(shPos1.x)<1.0 && abs(shPos1.y)<1.0)
    return v1;
  return 1.0;
  }
#endif

float shadowFactor(vec3 pos) {
#ifdef COMPUTE
  pos *= 100.0;
  vec4 shPos = scene.viewShadow[1]*vec4(pos,1.0);
  return calcShadow(shPos.xyz/shPos.w);
#else
  return 1.0;
#endif
  }

float shadowFactorMS(vec3 pos0, vec3 pos1) {
#ifdef COMPUTE
  pos0 *= 100.0;
  pos1 *= 100.0;

  float ret = 0;
  for(int i=0; i<8; ++i) {
    vec3 pos = mix(pos0,pos1,float(i)/8.0);
    vec4 shPos = scene.viewShadow[1]*vec4(pos,1.0);
    ret += calcShadow(shPos.xyz/shPos.w);
    }
  return ret/8.0;
#else
  return 1.0;
#endif
  }

#ifdef COMPUTE
vec3 inverse2(vec3 pos) {
  vec4 ret = scene.viewProjectInv*vec4(pos,1.0);
  return (ret.xyz/ret.w)/100.f;
  }
#else
vec3 inverse2(vec3 pos) {
  return inverse(pos);
  }
#endif

void main() {
#ifdef COMPUTE
  const ivec3 dstSz = imageSize(fogLut);
  const ivec3 dstUV = ivec3(gl_GlobalInvocationID.xy,0);
  if(dstUV.x>=dstSz.x || dstUV.y>=dstSz.y)
    return;
  const vec2  inPos  = ((vec2(dstUV.xy)+vec2(0.5))/vec2(dstSz.xy))*2.0-vec2(1.0);
  const int   steps  = dstSz.z;
#else
  const ivec3 dstUV  = ivec3(gl_FragCoord.xy,0);
  const int   steps  = 64;
#endif

  float dMin = 0.95;
  float dMax = 0.999;
  const vec3  pos1     = inverse2(vec3(inPos,dMax));
  const vec3  pos0     = inverse2(vec3(inPos,dMin));

  const float dist     = length(pos1-pos0);
  const vec3  dir      = normalize(pos1-pos0);

  const vec3  sunDir             = vec3(push.sunDir);
  const float cosTheta           = dot(dir, sunDir);
  const float miePhaseValue      = miePhase(cosTheta);
  const float rayleighPhaseValue = rayleighPhase(-cosTheta);

  vec3  scatteredLight = vec3(0.0);
  vec3  transmittance  = vec3(1.0);

  float prevT = 0;
  for(int i=0; i<steps; ++i) {
    float t      = float(i+0.3)/steps;
    if(i==0)
      t = 0.0;
    if(i+1==steps)
      t = 1.0;

    float dd     = (t-prevT)*dist;
    prevT = t;

    vec3  pos    = pos0 + t*dir*dist;
    vec3  newPos = pos  + vec3(0,RPlanet,0);

    vec3  rayleighScattering;
    vec3  extinction;
    float mieScattering;
    scatteringValues(newPos, rayleighScattering, mieScattering, extinction);

    vec3  sampleTransmittance = exp(-dd*extinction);

    float shadow              = shadowFactor(pos);
    //float shadow              = shadowFactorMS(pos0 + dir*dist*float(i)/float(steps), pos0 + dir*dist*t*float(i+1)/float(steps));
    vec3  sunTransmittance    = textureLUT(tLUT, newPos, sunDir) * shadow;
    vec3  psiMS               = textureLUT(mLUT, newPos, sunDir);

    vec3 rayleighInScattering = rayleighScattering*(rayleighPhaseValue*sunTransmittance + psiMS);
    vec3 mieInScattering      = mieScattering     *(miePhaseValue     *sunTransmittance + psiMS);
    vec3 inScattering         = (rayleighInScattering + mieInScattering);

    // Integrated scattering within path segment.
    vec3  scatteringIntegral  = (inScattering - inScattering * sampleTransmittance) / extinction;

    transmittance  *= sampleTransmittance;
    scatteredLight += scatteringIntegral*transmittance;

    store(ivec3(dstUV.xy,i), scatteredLight, transmittance);
    }
  }
