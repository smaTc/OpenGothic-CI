#version 460
#extension GL_ARB_separate_shader_objects : enable
#extension GL_GOOGLE_include_directive : enable

#if defined(RAY_QUERY)
#extension GL_EXT_ray_query : enable
#endif

#include "common.glsl"

layout(local_size_x = 8, local_size_y = 8) in;

layout(push_constant, std140) uniform PushConstant {
  mat4 mvp;
  mat4 mvpInv;
  };

layout(binding  = 0) uniform writeonly image2D result;

layout(binding  = 1) uniform sampler2D diffuse;
layout(binding  = 2) uniform sampler2D normals;
layout(binding  = 3) uniform sampler2D depthBuf;

#if defined(RAY_QUERY)
layout(binding  = 4) uniform accelerationStructureEXT topLevelAS;
#endif

const uint  numSamples = 32;
const float sphereLen  = 100;

ivec2 fragCoord;
ivec2 screenSize;
mat3  tangent;

shared uint vMin, vMax, vDiff;

bool isSolved(float occlusion, float dx) {
  vMin  = 256;
  vMax  = 0;
  vDiff = 0;
  barrier();

  if(occlusion>=0) {
    uint v = uint(occlusion*255+0.5);
    uint d = uint(dx       *255+0.5);

    atomicMin(vMin,  v);
    atomicMax(vMax,  v);
    atomicMax(vDiff, d);
    }
  memoryBarrierShared();
  barrier();

  uint diff = vMax - vMin;

  return (diff<4) || (vDiff<4);
  }

uint hash(uint x) {
  x += ( x << 10u );
  x ^= ( x >>  6u );
  x += ( x <<  3u );
  x ^= ( x >> 11u );
  x += ( x << 15u );
  return x;
  }

uint hash(uvec2 v) {
  return hash(v.x ^ hash(v.y));
  }

float floatConstruct(uint m) {
  const uint ieeeMantissa = 0x007FFFFFu; // binary32 mantissa bitmask
  const uint ieeeOne      = 0x3F800000u; // 1.0 in IEEE binary32

  m &= ieeeMantissa;                     // Keep only mantissa bits (fractional part)
  m |= ieeeOne;                          // Add fractional part to 1.0

  float  f = uintBitsToFloat( m );       // Range [1:2]
  return f - 1.0;                        // Range [0:1]
  }

// Sample i-th point from Hammersley point set of NumSamples points total.
// See: http://holger.dammertz.org/stuff/notes_HammersleyOnHemisphere.html
vec2 sampleHammersley(uint i, uint numSamples) {
  uint bits = bitfieldReverse(i);
  float vdc = float(bits) * 2.3283064365386963e-10; // / 0x100000000
  return vec2(float(i)/float(numSamples), vdc);
  }

vec3 sampleHemisphere(uint i, uint numSamples, float offsetAng) {
  const vec2  xi  = sampleHammersley(i,numSamples);
  const float u1p = sqrt(max(0.0, 1.0 - xi.y*xi.y));
  const float a   = M_PI*2.0*xi.x + offsetAng;
  return vec3(cos(a) * u1p, sin(a) * u1p, xi.y);
  }

vec3 inverse(vec3 pos) {
  vec4 ret = mvpInv*vec4(pos,1.0);
  return (ret.xyz/ret.w);
  }

vec3 project(vec3 pos) {
  vec4 ret = mvp*vec4(pos,1.0);
  return (ret.xyz/ret.w);
  }

float readZ(in vec2 posf) {
  return textureLod(depthBuf, posf.xy*0.5+vec2(0.5), 0).r;
  }

float sampleRadius(uint i, uint maxSmp) {
  return 0.5+float(i)/float(2*maxSmp);
  }

#if defined(RAY_QUERY)
bool rayTest(vec3 pos0, vec3 dp, float tMin, inout float len) {
  float rayDistance  = len;
  vec3  rayDirection = normalize(dp);
  if(rayDistance<=tMin)
    return true;

  //uint flags = gl_RayFlagsTerminateOnFirstHitEXT | gl_RayFlagsCullNoOpaqueEXT;
  uint flags = gl_RayFlagsOpaqueEXT;
  rayQueryEXT rayQuery;
  rayQueryInitializeEXT(rayQuery, topLevelAS, flags, 0xFF,
                        pos0, tMin, rayDirection, rayDistance);

  while(rayQueryProceedEXT(rayQuery))
    ;

  if(rayQueryGetIntersectionTypeEXT(rayQuery, true)==gl_RayQueryCommittedIntersectionNoneEXT)
    return false;

  len = rayQueryGetIntersectionTEXT(rayQuery, true);
  return true;
  }
#endif

vec2 calcOcclussion(const vec3 at0, const vec3 pos0, const float f0, uint seed, uint step) {
  float occlusion = 0, weightAll = 0;

  for(uint i=seed; i<numSamples; i+=step) {
    float r      = sampleRadius(i,numSamples);
    vec3  h      = sampleHemisphere(i,numSamples,f0);

    vec3  v      = tangent * h;
    vec3  at1    = project(pos0 + v*sphereLen*r);
    if(abs(at1.x)>1.0 || abs(at1.y)>1.0) {
#if defined(RAY_QUERY)
      vec3  pos1   = pos0 + v*sphereLen*r;
      vec3  dp     = (pos1-pos0);
      float len    = sphereLen*r;
      bool  isHit  = (rayTest(pos0,v,sphereLen*0.1,len));

      float lenQ   = len*len;
      float angW   = h.z;                                   // angle attenuation.
      float distW  = 1.0-min(lenQ/(sphereLen*sphereLen),1); // distance attenuation
      if(isHit)
        occlusion += angW*distW;
      weightAll += distW;
#endif
      continue;
      }
    float z      = readZ(at1.xy);
    vec3  at2    = vec3(at1.xy,z);
    vec3  pos2   = inverse(at2);
    vec3  dp     = (pos2-pos0);
    bool  isHit  = (z<at1.z);

    float lenQ   = dot(dp,dp);
    float angW   = h.z;                                   // angle attenuation.
    float distW  = 1.0-min(lenQ/(sphereLen*sphereLen),1); // distance attenuation
    if(isHit)
      occlusion += angW*distW;
    weightAll += distW;
    }

  return vec2(occlusion,weightAll);
  }

void main() {
  screenSize = ivec2(textureSize(depthBuf,0));
  fragCoord  = ivec2(gl_GlobalInvocationID.xy);

  const bool valid = all(lessThan(fragCoord, screenSize));

  if(valid) {
    vec3 norm = normalize(texelFetch(normals,fragCoord,0).xyz*2.0-vec3(1.0));
    // Compute a tangent frame and rotate the half vector to world space
    vec3 up   = abs(norm.z) < 0.999f ? vec3(0.0f, 0.0f, 1.0f) : vec3(1.0f, 0.0f, 0.0f);
    tangent[0] = normalize(cross(up, norm));
    tangent[1] = cross(norm, tangent[0]);
    tangent[2] = norm;
    }

  float occlusion = -1, prev= -1;
  vec2  occ       = vec2(0,0.0001);

  const vec2  inPos = vec2(2*fragCoord+ivec2(1,1))/vec2(screenSize)-vec2(1,1);
  const vec3  at0   = vec3(inPos.xy,readZ(inPos.xy));
  const vec3  pos0  = inverse(at0);
  const uint  h0    = hash(fragCoord);
  const float f0    = M_PI*2.0*floatConstruct(h0);

  int  maxSteps = 8;
  bool eval     = true;

  // TODO: linear depth
  {
    if(at0.z>=0.9995)
      eval = false; // sky
    if(at0.z>=0.998)
      maxSteps = 4; // far distance
    if(at0.z<=0.970)
      maxSteps = 4; // too close distance
  }

  int dbg = 0;
  for(int i=0; i<8; ++i) {
    if(valid && eval && i<maxSteps) {
      dbg = i;
      prev      = occlusion;
      occ      += calcOcclussion(at0, pos0, f0, i, 8);
      occlusion = (occ.x/occ.y)*M_PI;
      }
    if(i==0)
      continue;

    if(isSolved(occlusion, abs(occlusion-prev))) {
      // NOTE: 'break' here cause device-lost on Intel O_o
      eval = false;
      }
    }

  if(valid)
    imageStore(result, ivec2(gl_GlobalInvocationID.xy), vec4(occlusion));
  // imageStore(result, ivec2(gl_GlobalInvocationID.xy), vec4(float(dbg+1)/255.0));
  }
